class Game {
    field Alphabet alphabet;
    field Gallows gallows;
    field boolean isGameOver;
    field Array words;
    field GuessedWord guessedWord;
    field int mistakesCount;

    constructor Game new() {
        let isGameOver = false;
        let alphabet = Alphabet.new(); 

        do setWords();

        let mistakesCount = 6;
        return this;
    }

    method void startNewGame() {
        var Array pressedLetters;
        var char input;
        var boolean condition, hasSuccessInOpen;

        do setRandomWord();
        do alphabet.draw();
        do guessedWord.draw();
        let gallows = Gallows.new();

        while (~isGameOver) {
            let input = Keyboard.keyPressed();
            
            let condition = (input = 0) | checkIfCharWasPressed(input);
            if (~(condition)) {
                do alphabet.addPressedLetter(input);
                
                let hasSuccessInOpen = guessedWord.tryOpenLetter(input);
                if (~hasSuccessInOpen) {
                    let mistakesCount = mistakesCount - 1;
                    do gallows.drawNextStage();
                }

                do alphabet.crossOutLetter(input);
                 
                if (guessedWord.checkIfWin()) {
                    let isGameOver = true;
                    do won();
                }

                if (mistakesCount = 0) {
                    let isGameOver = true;
                    do lose();
                }
            }
            do Sys.wait(20);
        }
        return;
    }

    method void won() {
        do Sys.wait(600);
        do Screen.clearScreen();
        do Output.moveCursor(11,27); 
        do Output.printString("YOU WIN!!!");
        return;
    }

    method void lose() {
        do Sys.wait(300);
        do guessedWord.showLetters();
        do Sys.wait(1800);
        do Screen.clearScreen();
        do Output.moveCursor(11,27); 
        do Output.printString("YOU LOSE!!");
        return;
    }

    method boolean checkIfCharWasPressed(int charCode) {
        var int i;
        var int letterCode;
        var Array pressedLetters;

        let i = 0;
        let pressedLetters = alphabet.getPressedLetters();
        while (i < 26) {
            let letterCode = pressedLetters[i];
            if (~(letterCode = 0) & letterCode = charCode) {
                return true;
            }
            let i = i + 1;
        }
        return false;
    }

    method boolean isWordHasChar(int charCode) {
        var int i;
        var boolean success;
        var String word;

        let i = 0;
        let success = false;
        let word = guessedWord.getWord();
        while (i < word.length()) {
            if (word.charAt(i) = charCode) {
                let success = true;
            }
            let i = i + 1;
        }
        return success;
    }

    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }

    method void setRandomWord() {
        var int randomNum;
        var char key;
        var String word;

        let key = 0;

        do Output.moveCursor(11, 23); 
        do Output.printString("PRESS RANDOM KEY...");

        while (key = 0) {
            let key = Keyboard.keyPressed();
        }

        do Random.setSeed(key);
        let randomNum = Random.randRange(29);
        let word = words[randomNum];
        let guessedWord = GuessedWord.new(word);
        
        do Screen.clearScreen();
        do Sys.wait(200);
        return;
    }

    method void setWords() {
        let words = Array.new(30);
        let words[0] = "JEW";
        let words[1] = "DOG";
        let words[2] = "WOMAN";
        let words[3] = "RANDOM";
        let words[4] = "SUPER";
        let words[5] = "JACK";
        let words[6] = "HUMAN";
        let words[7] = "WATER";
        let words[8] = "BOTTLE";
        let words[9] = "RABBIT";
        let words[10] = "TIME";
        let words[11] = "YEAR";
        let words[12] = "PEOPLE";
        let words[13] = "THING";
        let words[14] = "WORLD";
        let words[15] = "MONEY";
        let words[16] = "FAMILY";
        let words[17] = "SYSTEM";
        let words[18] = "FRIEND";
        let words[19] = "GROUP";
        let words[20] = "RIGHT";
        let words[21] = "PLACE";
        let words[22] = "POWER";
        let words[23] = "NUMBER";
        let words[24] = "POINT";
        let words[25] = "LEVEL";
        let words[26] = "ORDER";
        let words[27] = "CHILD";
        let words[28] = "MUSIC";
        let words[29] = "SCALE";
        return;
    }
}